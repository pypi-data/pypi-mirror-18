/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;
    
    //required because the AttributeError clause in the PyObjectBase mixin masks the
    //generated version of this attribute.
    ...(`Member, `@m) { @m };

    ...(`RawGetItemByInt, arg) {
        @m[arg]
        };

    __class__: TupleType;

    __getitem__: PyBoundMethod(
        self,
        fun
        (@unnamed_args:(self, Slice(s)), *args) {
            if (s.step.__class__ is not NoneType)
                throw NotImplementedError(
                    PyString("step not yet implemented for tuple slicing")
                )

            // note there's no type checking on `s.start`, `s.stop`
            return PyTuple(self[s.start.@m, s.stop.@m])
            }
        (@unnamed_args:(self, PyInt(ix)), *args) {
            // note there's no type checking on rawInt here
            return self[ix.@m]
            }
        (@unnamed_args:(self, *args), *otherArgs) {
            throw TypeError(PyString("tuple indices must be integers or slice objects"))
            },
        `__getitem__
       );

    operator[]
    ({Int64} ix) {
        if (ix < -size(@m) or ix >= size(@m))
            throw IndexError(PyString("tuple index out of range"))
        
        return @m[ix]
        }
    (start, stop) {
        return @m[start, stop]
        };

    index: PyBoundMethod(
        self,
        fun(@unnamed_args:(self, value), *args) {
            for ix in sequence(size(self.@m)) {
                if (Bool(self.@m[ix] == value))
                    return PyInt(Int64(ix))
                }
            throw Exception(PyString("value is not in tuple"))
            },
        `index);

    __iter__: PyBoundMethod(
        self,
        fun(@unnamed_args:(self), *args) {
            for x in self.@m
                yield x
            },
        `__iter__);

    __len__: PyBoundMethod(
        self,
        fun(@unnamed_args:(self), *args) {
            PyInt(Int64(size(self.@m)))
            },
        `__len__);

    __str__: PyBoundMethod(
        self,
        fun(@unnamed_args:(self), *args) {
            let elts = []
            for e in self.@m
                elts = elts :: StrType(e).@m

            PyString("(" + ", ".join(elts) + ")")
            },
        `__str__);

    __eq__: PyBoundMethod(
        self, 
        fun
        (@unnamed_args:(self, PyTuple(other)), *args)
            {
            if (self.@m is () and other.@m is ())
                return PyBool(true)
            if (self.@m is () or other.@m is ())
                return PyBool(false)
            if ((self.@m[0] == other.@m[0]).@m) {
                return PyTuple(self.@m[1,]).__eq__(
                    PyTuple(other.@m[1,]))
                }
            return PyBool(false)
            }
        (@unnamed_args:(self, _), *args) {
            PyBool(false)
            },
        `__eq__
        );

    __ne__: PyBoundMethod(
        self, 
        fun
        (@unnamed_args:(self, PyTuple(other)), *args)
            {
            if (self.@m is () and other.@m is ())
                return PyBool(false)
            if (self.@m is () or other.@m is ())
                return PyBool(true)
            if ((self.@m[0] != other.@m[0]).@m)
                return PyBool(true)
                
            return PyTuple(self.@m[1,]).__ne__(
                PyTuple(other.@m[1,]))
            }
        (@unnamed_args:(self, _), *args) {
            PyBool(true)
            },
        `__ne__
        );    

    __lt__: PyBoundMethod(
        self,
        fun
        (@unnamed_args: (self, PyTuple(other)), *args) {
		    if (@m is () and other.@m is ())
			    return PyBool(false);
            if (@m is ())
                return PyBool(true)
            if (other.@m is ())
                return PyBool(false)
            if (Bool(@m[0] == other.@m[0]))
                return PyTuple(@m[1,]) < PyTuple(other.@m[1,])
            return @m[0] < other.@m[0]
            }
        (@unnamed_args: (self, _), *args) {
            PyBool(false)
            },
        `__lt__
        );

    __gt__: PyBoundMethod(
        self,
        fun
        (@unnamed_args: (self, PyTuple(other)), *args) {
		    if (@m is () and other.@m is ())
			    return PyBool(false);
            if (@m is ())
                return PyBool(false)
            if (other.@m is ())
                return PyBool(true)
            if (Bool(@m[0] == other.@m[0]))
                return PyTuple(@m[1,]) > PyTuple(other.@m[1,])
            return @m[0] > other.@m[0]
            }
         (@unnamed_args: (self, _), *args) {
            PyBool(true)
            },
        `__gt__
        );

    operator match(PyTuple _) { (self,) };

    operator match((#TupleAssign, expectedSize)) { 
        if (size(@m)==expectedSize) 
            return @m  
        if (size(@m) < expectedSize)
            throw ValueError(PyString("need more than " + String(size(@m)) + " values to unpack"))
        else
            throw ValueError(PyString("Too many values to unpack"))
        };

    operator+(PyTuple(other)) {
        PyTuple(@m + other.@m)
        };

    operator iterator() {
        for val in @m
            yield val
        };

    operator in (elt) {
        PyBool(elt in @m)
        };

    convert(Bool _) { @m is not () };

    mixin PyObjectBase;
    };

