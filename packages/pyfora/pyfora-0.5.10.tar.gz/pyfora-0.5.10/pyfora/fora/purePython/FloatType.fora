/***************************************************************************
    Copyright 2015 Ufora Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.
****************************************************************************/

"""The singleton object representing python's 'float' type."""
object {
    __bases__: PyTuple((Object,));
    
    __str__: PyBoundMethod(
        self,
        fun(@unnamed_args:(self), *args) {
            PyString("<type 'float'>")
            },
        `__str__);

    __call__: PyBoundMethod(
        self,
        fun
            (@unnamed_args:(self, PyFloat(...) other), *args) {
                other
                }
            (@unnamed_args:(self, PyInt(...) other), *args) { 
                PyFloat(Float64(other.@m))
                }
            (@unnamed_args:(self, PyString(...) other), *args) {
                try {
                    return PyFloat(Float64(other.@m))
                    }
                catch(...) {
                    throw ValueError(
                        PyString(
                            "could not convert string to float: " + other.@m
                            )
                        )
                    }
                }
            (@unnamed_args:(self, other), *args) {
                //return PyString("omg: " + String(other))

                let flt = 
                    try { other.__float__() }
                    catch(AttributeError(...)) {
                        throw TypeError(
                            PyString(
                                "could not convert argument to float"
                                )
                            )
                        };

                match (flt) with
                    (PyFloat(...) res) { return res }
                    (...) {
                        throw TypeError(PyString("__float__ returned non-float"))
                        }
                },
            `__call__
            );

    mixin Type;
    };
