/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/

class {
   member @generator;
   member @subGeneratorFun;

   canSplit: PyBoundMethod(
       self,
       fun(@unnamed_args:(self), *args) {
           return @generator.canSplit();
           },
       `canSplit);

   split: PyBoundMethod(
       self,
       fun(@unnamed_args:(self), *args) { 
           match (@generator.split()) with 
               (PyTuple(...) t) {
                   PyTuple((NestedGenerator(t[0], @subGeneratorFun),
                            NestedGenerator(t[1], @subGeneratorFun)))
                   }
               (PyNone(...) res) { res }
               (...) {
                   throw TypeError(PyString(
                       "Generator 'split' operations should return None or a pair of Generators"
                       ));}
               ;
           }, `split);

   associativeReduce: fun(valSoFar, add, merge, empty) {
      __without_stacktrace_augmentation { 
         return @generator.associativeReduce(
            valSoFar,
            fun(lst, val) { 
               __without_stacktrace_augmentation {
                  @subGeneratorFun(val).associativeReduce(lst, add, merge, empty)
                  }
               },
            merge,
            empty
            )
         }
      };

   isNestedGenerator: PyBoundMethod(
       self,
       fun(@unnamed_args:(self), *args) {
           PyBool(true)
           },
       `isNestedGenerator);

   childGenerators: PyBoundMethod(
       self,
       fun(@unnamed_args:(self), *args) {
           for key in @generator {
               yield @subGeneratorFun(key)
               }
           },
       `childGenerator);

   operator iterator() {
      for childGenerator in childGenerators() {
         for elt in childGenerator
            yield elt
         }
      };

   mixin PyGeneratorBase;
   };
