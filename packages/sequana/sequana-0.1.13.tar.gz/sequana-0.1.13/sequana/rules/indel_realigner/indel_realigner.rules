rule indel_realigner:
    """ Performs local realignment around indels to correct errors made by 
    genome aligners. It identifies regions where alignments may 
    potentially be improved, then realigns the reads in these regions.

    Required input:
        __indel_realigner__input: sorted bam file
        __indel_realigner__reference: fasta file
        __indel_realigner__ref_dict: reference dictionnary

    Required output:
        __indel_realigner__output: bam file
        __indel_realigner__intervals: intervals file. Must have ".intervals" 
            extension.

    Required parameter:
        config["gatk_bin"]: path of the gatk bin
        (default: "java -jar /path/to/GenomeAnalysisTK.jar" )

    Contributors:
        Sequana consortium
    """
    input:
        bam = __indel_realigner__input,
        ref = __indel_realigner__reference,
        ref_dict = __indel_realigner__ref_dict
    output:
        bam = __indel_realigner__output,
        intervals = __indel_realigner__intervals
    log:
        out = "logs/gatk/IndelRealigner/stdout.logs",
        err = "logs/gatk/IndelRealigner/stderr.logs"
    params:
        gatk = config["gatk_bin"]
    shell:
        """
        {params.gatk} -T RealignerTargetCreator -R {input.ref} \
        -I {input.bam} -o {output.intervals} > {log.out} 2> {log.err}

        {params.gatk} -T IndelRealigner -R {input.ref} -I {input.bam} \
        -targetIntervals {output.intervals} -o {output.bam} >> {log.out} \
        2>> {log.err}
        """
